/*
 * Copyright 2012-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def v = [
        guava: '15.0',
        slf4j: '1.6.6',
        servletApi: '2.5',
        jetty: '9.0.5.v20130815',
        jdt: '3.8.2.v20130121',
        spock: '0.6-groovy-1.8'
]

buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3"
    }
}

apply plugin: 'bintray'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
//    bintray.repo(repoOwner: 'davidmc24', repoName: 'gradle-plugins') // Uses credentials from gradle.properties
//    bintray.repo(repoOwner: 'WowCorp', repoName: 'AmazingSoftware') {
//        credentials {
//            username = 'joe'
//            password = '1234567890abcdef1234567890abcdef1234567890abcdef1234567890abcdef'
//        }
//    }
}

dependencies {
    compile     gradleApi()
    compile     group: 'com.google.guava', name: 'guava', version: v.guava
    compile     group: 'org.slf4j', name: 'slf4j-api', version: v.slf4j
    compile     group: 'org.eclipse.jetty', name: 'jetty-webapp',version: v.jetty
    compile     group: 'org.eclipse.jetty', name: 'jetty-plus', version: v.jetty
    runtime     group: 'org.eclipse.jetty', name: 'jetty-jsp', version: v.jetty
    runtime     group: 'org.eclipse.jetty', name: 'jetty-annotations', version: v.jetty
    testCompile group: 'org.spockframework', name: 'spock-core', version: v.spock
}

task wrapper(type: Wrapper, description: 'Gradle Wrapper task') {
    gradleVersion = '1.7'
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    scm {
        url "https://github.com/Khoulaiz/gradle-jetty-eclipse-plugin"
    }
    developers {
        developer {
            id "khoulaiz"
            name "Andreas Sahlbach"
            roles {
                role 'Developer' // might be 'Contributor' for contributors
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.with {
                    appendNode('description', 'A Gradle Jetty plugin for Eclipse Jetty')
                    appendNode('url', 'https://github.com/Khoulaiz/gradle-jetty-eclipse-plugin')
                    appendNode('inceptionYear', '2013')
                }
                root.children().last() + pomConfig
            }
        }
    }
    generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}

bintray {
    user = 'khoulaiz'
    key = bintray.key
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        name = "gradle-jetty-eclipse-plugin"
        licenses = ['Apache-2.0']
    }
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installing $project.name"
}

